plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.github.node-gradle.node' version '7.0.1'
	id 'jacoco'
}

group = 'com.idfc.bootcamp'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.0.2'
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.2.1'
	implementation 'javax.validation:validation-api:1.0.0.GA'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok:1.18.20'
	annotationProcessor 'org.projectlombok:lombok:1.18.20'
	implementation 'org.modelmapper:modelmapper:3.1.1'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mockito:mockito-core:3.12.4' // Replace with the latest version available
}

tasks.named('test') {
   useJUnitPlatform()
   systemProperties([
         // Configuration parameters to execute top-level classes in parallel but methods in same thread
         'junit.jupiter.execution.parallel.enabled': 'true',
         'junit.jupiter.execution.parallel.mode.default': 'same_thread',
         'junit.jupiter.execution.parallel.mode.classes.default': 'concurrent',
   ])
	finalizedBy jacocoTestReport
}

node {
	workDir = file("${project.projectDir}/src/main/frontend-ui/bookstoreapp/nodejs")
	npmWorkDir = file("${project.projectDir}/src/main/frontend-ui/bookstoreapp/npm")
}

task installFEDependencies(type: YarnTask){
	description = " read packge.json and install all jars"
	workingDir = file("${project.projectDir}/src/main/frontend-ui/bookstoreapp")
	args =["install"]
}

task buildFrontend(type: YarnTask, dependsOn: installFEDependencies){
	description = "Builds applciation for your frontend "
	workingDir = file("${project.projectDir}/src/main/frontend-ui/bookstoreapp")
	args =["build"]
}

task testFrontend(type: YarnTask, dependsOn: installFEDependencies){
	description = "Builds applciation for your frontend "
	workingDir = file("${project.projectDir}/src/main/frontend-ui/bookstoreapp")
	args =["test", "--watchAll=false"]
}

task copyFrontendFiles(type: Copy, dependsOn: [buildFrontend, resolveMainClassName]){
	from "${project.projectDir}/src/main/frontend-ui/bookstoreapp/build/"
	into "${project.projectDir}/build/resources/main/static"
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				minimum = 0.87
			}
			limit {
				counter = 'BRANCH'
				minimum = 0.7
			}
		}
	}
}

jacocoTestReport {
	reports {
		csv.required = true
	}
}


task buildJar(type: Jar) {
	dependsOn copyFrontendFiles
}
